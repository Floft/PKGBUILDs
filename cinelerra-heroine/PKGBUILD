# Maintainer: Garrett <floft.net/contact>
pkgname=cinelerra-heroine
pkgver=4.4
pkgrel=5
pkgdesc="Cinelerra from the HeroineWarrior.com (64-bit)"
arch=('x86_64')
url="http://www.heroinewarrior.com/cinelerra.php"
license=('GPL')
depends=('libpng' 'freeglut' 'libxv' 'termcap' 'libva' 'openexr')
makedepends=('yasm' 'nasm' 'libtool')
conflicts=('cinelerra-cv')
source=(https://sourceforge.net/projects/heroines/files/cinelerra-$pkgver-src.tar.xz
        quicktime.patch
        shapewipe.patch
        plugin_dir.patch
        speech_tools.patch
        texi2html.patch
        gpac.patch
        libavcodec.patch
        cinelerra.patch)
md5sums=('252d811546025470ae4d7fa31c1f52d4'
         '788b9e54fc7ae45b0154d129662a6b1d'
         '6955fcb04e9e3b745a1c34fec77b2be0'
         'cf9861e808948fe8ea4d4be27765ffa5'
         '1d15ee1b2e783a4f8ad8c02d53a77439'
         '431503db853032e14ade0438ed3eea29'
         'cefa6a8efe5ba5bc00ad9fc5649dd25c'
         '4dd7fd1184dfe3813a8821d47ef70a55'
         '2b62004366f8eaa7f27f1981765205b1')
sha1sums=('e383f66103e4b1b53623b2d02dc6655d465384b1'
          'fedfecc17a458b382b3e646945a19a43e968f37e'
          'a3ceb3415717c2186c35f6b35f395447c9014f72'
          '6d6bf19a71346870e38e67c0ed7bd19742551f02'
          '0bc32f81410ceb93f380f3bbeede4e10de56fb2c'
          '6f7595318237272b0d31a296719466884e6aeb95'
          '8c1cc485a5e80430eface70a25669b4a796f5579'
          '5a7f9c673d9d85ab00fa8002190a949e625b859d'
          '582756dcecbde59a44e7b36673fb589471ec0fce')

build() {
  cd "$srcdir/cinelerra-$pkgver"

  # fixes to make it compile
  patch -Np1 -i "$srcdir/quicktime.patch"
  patch -Np1 -i "$srcdir/shapewipe.patch"
  patch -Np1 -i "$srcdir/speech_tools.patch"
  patch -Np1 -i "$srcdir/texi2html.patch"
  patch -Np1 -i "$srcdir/gpac.patch"
  patch -Np1 -i "$srcdir/libavcodec.patch"
  patch -Np1 -i "$srcdir/cinelerra.patch"
  
  # fix problems with Cinelerra_plugins in read-only directories
  patch -Np1 -i "$srcdir/plugin_dir.patch"

  ./configure
  make
}

package() {
  cd "${srcdir}/cinelerra-$pkgver"

  # makes repackaging work
  [ ! -e "bin/cinelerra" ] && \
  	make -C cinelerra && \
	make -C plugins

  make install

  # install puts it in bin/
  rm -rf "$pkgdir/usr/lib/cinelerra/"
  mkdir -p "$pkgdir/usr/lib/cinelerra/"
  mv bin/* "$pkgdir/usr/lib/cinelerra/"

  mkdir -p "$pkgdir/usr/bin/"
  ln -s /usr/lib/cinelerra/cinelerra "$pkgdir/usr/bin/cinelerra"
}
