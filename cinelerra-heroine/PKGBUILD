pkgname=cinelerra-heroine
pkgver=4.3
pkgrel=1
pkgdesc="Cinelerra from the HeroineWarrior.com (64-bit)"
arch=('x86_64')
url="http://www.heroinewarrior.com/cinelerra.php"
license=('GPL')
depends=('libpng' 'freeglut' 'libxv' 'termcap' 'libva')
makedepends=('yasm' 'nasm' 'libtool')
conflicts=('cinelerra-cv')
source=(https://sourceforge.net/projects/heroines/files/cinelerra-$pkgver-src.tar.bz2
	quicktime.patch)
md5sums=('42def96800668fa4a029bd1a982a4648'
	 'c555059768637c1456b71a36748ed81b')
sha256sums=('8f0fa7b8b33356f984e40d55bb362ac87651328c30741b30d1fec864e32dba4c'
	    'bd8c849582ccf8608951e4755983b9c56bcbd73e1ea8a09a922a5ed21419c255')

build() {
  cd "$srcdir/cinelerra-$pkgver"

  #make quicktime/ compile
  patch -Np1 -i "$srcdir/quicktime.patch"

  ./configure
  make
}

package() {
  cd "${srcdir}/cinelerra-$pkgver"

  #makes repackaging work
  [ ! -e "bin/cinelerra" ] && \
  	make -C cinelerra && \
	make -C plugins

  make install

  #install puts it in bin/
  rm -rf "$pkgdir/usr/lib/cinelerra/"
  mkdir -p "$pkgdir/usr/lib/cinelerra/"
  mv bin/* "$pkgdir/usr/lib/cinelerra/"

  #no longer do it in .install
  mkdir -p "$pkgdir/usr/bin/"
  ln -s /usr/lib/cinelerra/cinelerra "$pkgdir/usr/bin/cinelerra"
}
