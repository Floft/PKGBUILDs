# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=cinelerra-cv
pkgver=20110703
pkgrel=1
pkgdesc="A complete audio and video production environment for Linux."
arch=('i686' 'x86_64')
url="http://cinelerra.org"
license=('GPL')
depends=('e2fsprogs' 'a52dec' 'fftw' 'lame' 'libavc1394' 'libiec61883'
   'libraw1394' 'libsndfile' 'libvorbis' 'libogg' 'libpng>=1.4.0'
   'libjpeg>=8' 'libtiff' 'esound' 'mjpegtools' 'openexr' 'x264'
   'libxxf86vm' 'ffmpeg' 'libxv' 'faad2' 'faac' 'libtheora' 'freetype2')
makedepends=('automake>=1.7' 'autoconf>=2.57' 'libtool' 'git' 'nasm'
       'libgl' 'mesa')
optdepends=('libgl: needed for the X11-OPENGL video driver support')
options=('!libtool')
source=(cinelerra-v4l1_removal.patch
	gimplayermodes.patch
	http://www.floft.net/uploads/scripts/cinelerra/screen.png
	http://www.floft.net/uploads/scripts/cinelerra/softlight.png)
md5sums=('bfa85e20809429d88eba4ab83e569612'
	 'd81fefeebe3c999ad78e863f34338cf5'
	 'dc3b562fb3301a16d458a84c2875380d'
	 '692403e6fc04301b577cd40bcb5d30fa')

_gitroot="git://git.cinelerra.org/j6t/cinelerra.git"
_gitname="cinelerra"

build() {
  unset LDFLAGS
  export CPPFLAGS="-D__STDC_CONSTANT_MACROS"

  #gcc 4.6 workaround
  export CFLAGS="$CFLAGS -Wwrite-strings"

  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/$_gitname-build"
  cp -r "${srcdir}/$_gitname" "${srcdir}/$_gitname-build"
  cd "${srcdir}/$_gitname-build"

  #make work with kernel >=2.6.38
  patch -Np1 -i ${srcdir}/cinelerra-v4l1_removal.patch

  #gimp overlay modes
  #from: http://www.pegtop.net/delphi/articles/blendmodes/
  #      http://docs.gimp.org/en/gimp-concepts-layer-modes.html
  patch -Np1 -i ${srcdir}/gimplayermodes.patch
  for i in softlight.png screen.png; do
    noext=${i//.*/}
    cp ${srcdir}/$i ./cinelerra/data/mode_$noext.png
    cp ${srcdir}/$i ./plugins/suv/data/mode_$noext.png
    cp ${srcdir}/$i ./plugins/bluedottheme/data/mode_$noext.png
    cp ${srcdir}/$i ./plugins/defaulttheme/data/mode_$noext.png
  done

  sed -i -e '/Debian/d' admin/nasm

  # If you don't need OpenGL comment out the next line.
  sed -i '/\/X11R6/s///' configure.in

  ./autogen.sh

  if [ "$CARCH" = "x86_64" ]; then
    ./configure --prefix=/usr --with-buildinfo=git/recompile --with-external-ffmpeg --disable-mmx --enable-opengl
  else
    ./configure --prefix=/usr --with-buildinfo=git/recompile --with-external-ffmpeg --enable-mmx --enable-opengl
  fi

  make
}

package() {
  cd ${_gitname}-build

  make DESTDIR=${pkgdir} install
}
