# Maintainer: Garrett <floft.net/contact>
pkgname=cinelerra-heroine-gimpoverlaymodes
pkgver=4.4
pkgrel=1
pkgdesc="Cinelerra from the HeroineWarrior.com (64-bit)"
arch=('x86_64')
url="http://www.heroinewarrior.com/cinelerra.php"
license=('GPL')
depends=('libpng' 'libpng12' 'freeglut' 'libxv' 'termcap' 'libva')
makedepends=('yasm' 'nasm' 'libtool')
conflicts=('cinelerra-cv' 'cinelerra-heroine')
provides=('cinelerra-heroine')
source=(https://sourceforge.net/projects/heroines/files/cinelerra-$pkgver-src.tar.xz
	quicktime.patch
	speech_tools.patch
	gimpoverlaymodes-heroine.patch
	http://www.floft.net/uploads/scripts/cinelerra/screen-black.png
	http://www.floft.net/uploads/scripts/cinelerra/screen-gray.png
	http://www.floft.net/uploads/scripts/cinelerra/softlight-black.png
	http://www.floft.net/uploads/scripts/cinelerra/softlight-gray.png)
md5sums=('252d811546025470ae4d7fa31c1f52d4'
	 'c555059768637c1456b71a36748ed81b'
	 '427b433561dd5f3240fcfc4a407146ce'
	 '4ae8c471448d70f943deaa079bf08189'
	 '1069981fe60347b885acb8731be4a2b5'
	 'd255dc6de2d67e90c3be5bbd30892abd'
	 '6e0fb2db3ec864db6a9157a855e05e12'
	 'b11ef214d7d2317843e834953dc358da')
sha1sums=('e383f66103e4b1b53623b2d02dc6655d465384b1'
	  'cd1e26d9cec3a3ea47e3f04b90fdbd4e0144fdf9'
	  '463b5f84a50bd002f2e0750a53649f01b800b293'
	  'dee2143d254b1fec8d3b8eb8257ada6d795125f3'
	  'a4c45f2991edd9ebb12ba478da206e0e8d581712'
	  '2081326b92e37d88ccf7d1393816747a21bddd88'
	  '90c345ec1746e2e466acfa5040dc6e8f516a87f6'
	  '527005215fd48019085b44d77014b2546e114a52')

build() {
  cd "$srcdir/cinelerra-$pkgver"

  #fixes to make it compile
  patch -Np1 -i "$srcdir/quicktime.patch"
  patch -Np1 -i "$srcdir/speech_tools.patch"

  #add layer modes
  patch -Np1 -i "$srcdir/gimpoverlaymodes-heroine.patch"
  cp "$srcdir/screen-black.png" cinelerra/data/mode_screen.png
  cp "$srcdir/softlight-black.png" cinelerra/data/mode_softlight.png
  cp "$srcdir/screen-gray.png" plugins/suv/data/mode_screen.png
  cp "$srcdir/softlight-gray.png" plugins/suv/data/mode_softlight.png

  ./configure
  make
}

package() {
  cd "${srcdir}/cinelerra-$pkgver"

  #makes repackaging work
  [ ! -e "bin/cinelerra" ] && \
  	make -C cinelerra && \
	make -C plugins

  make install

  #install puts it in bin/
  rm -rf "$pkgdir/usr/lib/cinelerra/"
  mkdir -p "$pkgdir/usr/lib/cinelerra/"
  mv bin/* "$pkgdir/usr/lib/cinelerra/"

  mkdir -p "$pkgdir/usr/bin/"
  ln -s /usr/lib/cinelerra/cinelerra "$pkgdir/usr/bin/cinelerra"
}
