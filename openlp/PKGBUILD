# Maintainer: Garrett <floft.net/contact>
pkgname=openlp
pkgver=2.0
pkgrel=1
pkgdesc="Church presentation software."
arch=('any')
url='http://openlp.org/'
license=('GPLv2')
depends=('qt' 'python2' 'python2-pyqt' 'python2-distribute' 'phonon' 'python2-chardet'
	'python2-lxml' 'python2-beautifulsoup3' 'python2-sqlalchemy' 'python2-sip'
	'python2-pysqlite-legacy' 'python2-pyenchant' 'python2-mako' 'python2-migrate')
optdepends=('libreoffice-impress: display impress presentations')
install=openlp.install
source=(http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/OpenLP-$pkgver.tar.gz openlp.sh)
md5sums=('52785012d63125867bcfefb60f9769b6'
         'bb78637dd085de567dc8aea8699939bf')
sha1sums=('7d198c8697c91e98cd9c0fe3a0903332fade2cf5'
	  'f44f1b0070cbd3beb5c80ca5e502b68501d38f9c')

build() {
  cd "$srcdir/OpenLP-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  mv "$pkgdir/usr/bin/openlp"{.pyw,}

  echo "$pkgver" > "$pkgdir/usr/lib/python2.7/site-packages/openlp/.version"

  install -Dm0755 "$srcdir/openlp.sh" "$pkgdir/etc/profile.d/openlp.sh"
  install -Dm0644 "resources/openlp.desktop" "$pkgdir/usr/share/applications/openlp.desktop"
  install -Dm0644 "resources/images/openlp-logo.svg" "$pkgdir/usr/share/pixmaps/openlp.svg"
  install -Dm0644 "resources/images/openlp-logo-48x48.png" "$pkgdir/usr/share/pixmaps/openlp.png"

  #translations
  tsrcdir="$srcdir/OpenLP-$pkgver/resources/i18n"
  tdestdir="$pkgdir/usr/share/$pkgname/i18n"
  mkdir -p "$tdestdir"
  cd "$tsrcdir"
  
  for file in *.ts; do
    lconvert -i "$file" -o "$tdestdir/${file%%ts}qm"
  done
}
