# Maintainer: Garrett <floft.net/contact>
pkgname=worldwind
pkgver=1.5.1
pkgrel=1
pkgdesc="an open source 3D interactive world viewer"
arch=('i686' 'x86_64')
url="http://worldwind.arc.nasa.gov/java/"
license=('custom')
optdepends=('libtxc_dxtn: radeon and nouveau support')
source=('http://builds.worldwind.arc.nasa.gov/worldwind-releases/1.5/builds/worldwind-1.5.1.zip')
md5sums=('8e7f3085028ce01b005153e53bf481c1')
sha256sums=('cc452953ebf987cd558f835efe36c6a187093bb17e06443fea7e761557d2107d')

if [[ $CARCH == i686 ]]; then
    depends=('jogl')
else
    source+=('http://worldwind.arc.nasa.gov/java/jogl/webstart/jogl-natives-linux-amd64.jar'
        'http://worldwind.arc.nasa.gov/java/jogl/webstart/gluegen-rt-natives-linux-amd64.jar')
    md5sums+=('6bd6dff3409fbba29738c56e3e36650d'
             'df40663205b7ee954434de97811ff93c')
    sha256sums+=('20ebe1be1a6a34c749831d9e3f7447486f7dbd704b12f4080fc1542b4699b456'
                'eea19e049f59cb5acb60ae51f2be2239890f5ef5d7d9b0535169072559c490e0')
fi

package() {
	cd "$srcdir"

	mkdir -p "$pkgdir/usr/bin/"
	cat > "$pkgdir/usr/bin/worldwind" <<EOF
#!/bin/bash
cd '/usr/share/worldwind/';
export LD_LIBRARY_PATH="$LD_LIBRARY_PATH:/usr/lib/jogl"
java -Xmx512m -Djava.library.path=. -classpath ./src:./classes:./worldwind.jar:./worldwindx.jar:./jogl.jar:./gluegen-rt.jar:./gdal.jar gov.nasa.worldwindx.examples.ApplicationTemplate
EOF
	chmod +x "$pkgdir/usr/bin/worldwind"

	mkdir -p  "$pkgdir/usr/share/$pkgname"
	cp -r ./* "$pkgdir/usr/share/$pkgname/"
	
	mkdir -p "$pkgdir/usr/share/licenses/$pkgname/"
	install -Dm644 "NASA_Open_Source_Agreement_1.3.txt" \
		"$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	
	if [[ $CARCH == i686 ]]; then
		rm "$pkgdir/usr/share/$pkgname/lib-external/gdal/libgdalalljni64.so"
    fi
}
