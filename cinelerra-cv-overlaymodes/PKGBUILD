# $Id: PKGBUILD 73468 2012-07-08 09:29:46Z ibiru $
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Garrett <floft.net/contact>
pkgname=cinelerra-cv-overlaymodes
pkgver=2.2
_gitrel=2.2.0
pkgrel=1
epoch=1
pkgdesc="Professional video editing and compositing environment"
arch=('x86_64' 'i686')
url="http://cinelerra.org/"
license=('GPL')
depends=('e2fsprogs' 'libavc1394' 'libiec61883' 'freetype2'
         'libsndfile' 'libtiff' 'mjpegtools>=2.0.0' 'fftw' 'a52dec'
         'ffmpeg' 'libxv' 'faad2' 'faac' 'mesa' 'openexr')
makedepends=('git' 'nasm')
options=('!libtool')
provides=('cinelerra-cv')
conflicts=('cinelerra-cv')
source=('v4l1_removal.patch'
        'ffmpeg_api.patch'
        'gimplayermodes.patch'
        'http://floft.net/uploads/scripts/cinelerra/screen-gray.png'
        'http://floft.net/uploads/scripts/cinelerra/softlight-gray.png')
sha256sums=('50f232fb034bb8aba496e2fb078cdb33d27bb09ba7cb33d99715e171811cff12'
            'd7614ab75d7b0c58142fc762d8dcc3120c3bac6003d18e81fb26b06fd665e629'
			      '63d0d9cbd8242784bf3affbed31c487af135e42cd6a966d07d3d4f5b107ebb3c'
			      'c2047c009f80a674e96ef27de8e3ef7427bb6a8bdff73799e9c2a60e39b716d8'
			      'c9c4aa59496f37332d6dfc0737a4e6ca64aa3aa6f3241f19816ee5f62490c0fe')
_gituri="git://git.cinelerra.org/CinelerraCV.git"
_gitdir="cinelerra"

confit() {
  ./configure --prefix=/usr \
              --with-buildinfo=git/recompile \
              --with-external-ffmpeg \
              --enable-opengl \
              --disable-esd $@
}

build() {
  msg "Connecting to GIT server...."

  if [ -d $_gitdir ] ; then
    cd $_gitdir && git pull origin
    msg "The local files are updated."
  else
    git clone $_gituri -b rel$_gitrel $_gitdir
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitdir-build"
  cp -r "$srcdir/$_gitdir" "$srcdir/$_gitdir-build"
  cd "$srcdir/$_gitdir-build"

  # gcc 4.6 workaround
  export CFLAGS="$CFLAGS -Wwrite-strings"

  unset LDFLAGS
  export CPPFLAGS="-D__STDC_CONSTANT_MACROS"

  # v4l1 removal patch
  patch -Np1 -i "$srcdir/v4l1_removal.patch"

  # new ffmpeg api patch
  patch -Np1 -i "$srcdir/ffmpeg_api.patch"

#gimp overlay modes
  #from: http://www.pegtop.net/delphi/articles/blendmodes/
  #      http://docs.gimp.org/en/gimp-concepts-layer-modes.html
  patch -Np1 -i "$srcdir/gimplayermodes.patch"
  for i in softlight-gray.png screen-gray.png; do
    noext="${i//-*/}"
    cp "$srcdir/$i" "./cinelerra/data/mode_$noext.png"
    cp "$srcdir/$i" "./plugins/suv/data/mode_$noext.png"
    cp "$srcdir/$i" "./plugins/bluedottheme/data/mode_$noext.png"
    cp "$srcdir/$i" "./plugins/defaulttheme/data/mode_$noext.png"
  done

  sed -i -e '/Debian/d' admin/nasm

  # if you don't need OpenGL comment out the next line (and no mesa dep)
  sed -i '/\/X11R6/s///' configure.in

  ./autogen.sh

  if [ "$CARCH" = 'x86_64' ]; then
    confit --disable-mmx
  else
    confit --enable-mmx
  fi

  make
}

package() {
  cd "$srcdir/$_gitdir-build"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
