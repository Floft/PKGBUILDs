# $Id: PKGBUILD 73315 2012-07-06 08:33:12Z lfleischer $
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Ali H. Caliskan <ali.h.caliskan AT gmail DOT com>
# Contributor: Ryan Coyner <rcoyner@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=mixxx
pkgver=1.10.1
pkgrel=1
pkgdesc="Free, open source software for digital DJ'ing."
arch=('i686' 'x86_64')
url='http://www.mixxx.org'
license=('GPL')
depends=('fftw' 'libid3tag' 'libmad' 'libogg' 'libshout' 'libsndfile' 'portaudio' 'portmidi'
         'qt>=4.5' 'taglib' 'qtwebkit')
makedepends=('mesa' 'scons' 'libshout')
source=("http://downloads.mixxx.org/${pkgname}-${pkgver}/${pkgname}-${pkgver}-src.tar.gz"
	"volume-fade2.patch")
md5sums=('765bc5216ec8102c2186dd3ea80187a9'
	'd7fc2c929a0c6b9a97d9bb5dccfc7ece')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 < "$srcdir/volume-fade2.patch"
  scons qtdir=/usr/lib/qt prefix=/usr install_root="${pkgdir}/usr"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  scons qtdir=/usr/lib/qt prefix=/usr install_root="${pkgdir}/usr" install
}
